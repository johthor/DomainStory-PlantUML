@startebnf
title DomainStory-PlantUML Syntax

domainStory = { actors | workObjects | boundaries | activities }-;

actors = ( ("Person" | "Group" | "System"), "(", elementParameters, ")" ) |\
( "Actor", "(", "$kind", "$iconName", elementParameters, ")" );

workObjects = ( ("Document" | "Folder" | "Call" | "Email" | "Conversation" | "Info"), "(", elementParameters, ")" ) |\
( "Object", "(", "$kind", "$iconName", elementParameters, ")" );

elementParameters = "$name",[ "$label", ["$tag", ["$note"] ] ], { ?$tag? | ?$note? | ?$shape? | ?$scale? | ?$color? | ?$background? };

boundaries = "Boundary", "(", "$name", [ "$label", ["$tag", ["$note"] ] ], { ?$tag? | ?$note? | ?$shape? | ?$background? },")";

activities = "activity", "(", "$step", "$subject", "$predicate", "$object", [ "$post", ["$target", [ "$objectArr", ["$targetArr"] ] ] ], activityParameters,")";
activityParameters = { ?$tag? | ?$note? | ?$shape? | ?$scale? | ?$color? | ?$background? |\
?$targetTag? | ?$targetNote? | ?$targetShape? | ?$targetScale? | ?$targetColor? | ?$targetBackground? };
@endebnf
