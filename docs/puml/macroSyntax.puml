@startebnf
title DomainStory-PlantUML Syntax

domainStory = { actors | workObjects | activities | boundaries }-;

actors = ( ("Person" | "Group" | "System"), "(", elementPositionalParameters, elementKeywordParameters, ")" )
    | ( ("namedPerson" | "namedGroup" | "namedSystem"), "(", "$name", [ elementPositionalParameters ], elementKeywordParameters, ")" )
    | ( "Actor", "(", "$kind", "$iconName", elementPositionalParameters, elementKeywordParameters, ")" );

workObjects = ( ("Document" | "Folder" | "Call" | "Email" | "Conversation" | "Info"), "(", elementPositionalParameters, elementKeywordParameters, ")" )
    | ( ("namedDocument" | "namedFolder" | "namedCall" | "namedEmail" | "namedConversation" | "namedInfo"), "(", "$name", [ elementPositionalParameters ], elementKeywordParameters, ")" )
    | ( "Object", "(", "$kind", "$iconName", elementPositionalParameters, elementKeywordParameters, ")" );

elementPositionalParameters = "$label", ["$tag", ["$note"] ];

elementKeywordParameters = [ "<= 6" * ( ?$tag? | ?$note? | ?$shape? | ?$scale? | ?$color? | ?$background? ) ];
(* each keyword parameter should only be set at most once *)

activities = ( "startActivity", "(", "$step", "$subject", "$predicate", "$object", [ "$arrow" ], ")" )
    | ( "append", "(", "$action", "$object", [ "$arrow" ], ")" )
    | ( "split", "(", "$action", "$object", [ "$arrow" ], ")" )
    | ( "splitAgain", "(", "$action", "$object", [ "$arrow" ], ")" )
    | ( "continue", "(", "$action", "$object", [ "$arrow" ], ")" )
    | ( "continueFromBefore", "(", "$action", "$object", [ "$arrow" ], ")" )
    | ( "activity", "(", "$step", "$subject", "$predicate", "$object",\
        [ "$post", ["$target", [ "$objectArr", ["$targetArr"] ] ] ], activityKeywordParameters,")" );

activityKeywordParameters = [ "<= 14" * ( ?$objectArr? |?$tag? | ?$note? | ?$shape? | ?$scale? | ?$color? | ?$background?
    | ?$targetArr? | ?$targetTag? | ?$targetNote? | ?$targetShape? | ?$targetScale? | ?$targetColor? | ?$targetBackground? ) ];
(* each keyword parameter should only be set at most once *)

boundaries = "Boundary", "(", "$name", [ "$label", ["$tag", ["$note"] ] ], boundaryKeywordParameters, ")";

boundaryKeywordParameters = [ "<= 4" * ( ?$tag? | ?$note? | ?$shape? | ?$background? ) ];
(* each keyword parameter should only be set at most once *)
@endebnf
